yixxas

high

# Users can lose funds when calling `extendLock()`

## Summary
User that tries to extend their lock duration will have their tokens burned in some cases. This also causes them to be unable to withdraw their original deposit tokens.

## Vulnerability Detail

1. `extendLock()` checks for the new duration with `uint256 duration = maxLockDuration.min(uint256(userDeposit.end - block.timestamp) + increaseDuration)`
2. `mintAmount` is then calculated with `uint256 mintAmount = userDeposit.amount * getMultiplier(duration) / 1e18`
3. It then compares the previously minted amount with this new amount and mint or burn depending on the difference.

This calculation will cause user to lose tokens when the duration they extend is less than the amount of time passed. I illustrate this with a simple example.

A user first `deposit()` some amount and with duration of `1 year` and mints `x_num_of_tokens` depending on the curve. Now, right before the lock ends, at `0.99999 year`, this same user chooses to call `extendLock()` for `1 week`. The new duration calculated is `maxLockDuration.min(uint256(userDeposit.end - block.timestamp) + increaseDuration)` and this would be very close to `increaseDuration` which is 1 week in our case. `mintAmount` is calculated with this 1 week duration. Assuming curve does not change, the previously calculated `mintAmount` using `1 year` is much higher than this new one using `1 week`. End result is that `0.99999 year`'s worth of user's token is burnt. Now, user will not even be able to withdraw their original deposit tokens without the pool tokens as it will revert due to `_burn()` in `withdraw()`.

## Impact
Users who call `extendLock()` will lose their funds. How much of it depends on the point in time in which they calls it. The closer it is to expiration, the more funds are lost.

## Code Snippet

[TimeLockPool.sol#L148](https://github.com/sherlock-audit/2022-10-merit-circle/blob/main/merit-liquidity-mining/contracts/TimeLockPool.sol#L148)
```solidity
    function extendLock(uint256 _depositId, uint256 _increaseDuration) external {
        // Check if actually increasing
        if (_increaseDuration == 0) {
            revert ZeroDurationError();
        }

        Deposit memory userDeposit = depositsOf[_msgSender()][_depositId];

        // Only can extend if it has not expired
        if (block.timestamp >= userDeposit.end) {
            revert DepositExpiredError();
        }
        
        // Enforce min increase to prevent flash loan or MEV transaction ordering
        uint256 increaseDuration = _increaseDuration.max(MIN_LOCK_DURATION);
        
        // New duration is the time expiration plus the increase
        uint256 duration = maxLockDuration.min(uint256(userDeposit.end - block.timestamp) + increaseDuration);

        uint256 mintAmount = userDeposit.amount * getMultiplier(duration) / 1e18;

        // Multiplier curve changes with time, need to check if the mint amount is bigger, equal or smaller than the already minted
        
        // If the new amount if bigger mint the difference
        if (mintAmount > userDeposit.shareAmount) {
            depositsOf[_msgSender()][_depositId].shareAmount =  mintAmount;
            _mint(_msgSender(), mintAmount - userDeposit.shareAmount);
        // If the new amount is less then burn that difference
        } else if (mintAmount < userDeposit.shareAmount) {
            depositsOf[_msgSender()][_depositId].shareAmount =  mintAmount;
            _burn(_msgSender(), userDeposit.shareAmount - mintAmount);
        }

        depositsOf[_msgSender()][_depositId].start = uint64(block.timestamp);
        depositsOf[_msgSender()][_depositId].end = uint64(block.timestamp) + uint64(duration);
        emit LockExtended(_depositId, _increaseDuration, _msgSender());
    }
```

## Tool used

Manual Review

## Recommendation

I recommend removing the `extendLock` function. There are many factors to consider when implementing such a function as there are a few variables to consider here.
1. When attempting to extend the lock of a deposit, since curve calculation is exponential, do we want to include the total time inclusive of the extension or reward them for just the extended duration? Note that the former opens up new attack vectors.
2. Curve reward can change at anytime. Do we punish users for extending to a longer duration, but yet receive a lower reward due to the curve if the total reward is recomputed based on this new curve.
3. A mixture of (1) and (2) can make this even more complicated.  
