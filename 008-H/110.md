ElKu

high

# extendLock function in TimeLockPool contract overwrites previously accumulated token shares

## Summary

When the user calls the `extendLock` function to extend the deposit expiry timestamp, the contract calculates the new amount of shares based on the time for which it is getting extended. The tokens accumulated so far gets overwritten and ignored. 

## Vulnerability Detail

Let's take an example:
1. `Alice` deposits 1000 tokens for a duration of 3 years.
2. The contract mints, lets say 1200 tokens(based on `getMultiplier` function's return value) on her behalf. We assume that `getMultiplier` returns a value of 1.2 when the input duration is 3 years.
3. At the end of the 3rd year, before the deposit was going to get expired, `Alice` calls the `extendLock` function and passes `_increaseDuration` as 2 years. Meaning she want to extend it for a further 2 years. In total she would have deposited for 5 years.
4. Looking at the [extendLock](https://github.com/sherlock-audit/2022-10-merit-circle/blob/main/merit-liquidity-mining/contracts/TimeLockPool.sol#L148-L184) code:
```solidity
	uint256 increaseDuration = _increaseDuration.max(MIN_LOCK_DURATION);
        uint256 duration = maxLockDuration.min(uint256(userDeposit.end - block.timestamp) + increaseDuration);
        uint256 mintAmount = userDeposit.amount * getMultiplier(duration) / 1e18;
```
We can calculate that:
`increaseDuration` = 2 years.
`duration` = min(4 years, 0 +2 years) = 2 years.
Lets say: `getMultiplier(2 years)` = 1.1.
Then `mintAmount` = 1000 * 1.1 = 1100. 

This freshly calculated `mintAmount` is compared with the current number of shares `Alice` has:
```solidity
	if (mintAmount > userDeposit.shareAmount) {
            depositsOf[_msgSender()][_depositId].shareAmount =  mintAmount;
            _mint(_msgSender(), mintAmount - userDeposit.shareAmount);
        // If the new amount is less then burn that difference
        } else if (mintAmount < userDeposit.shareAmount) {
            depositsOf[_msgSender()][_depositId].shareAmount =  mintAmount;
            _burn(_msgSender(), userDeposit.shareAmount - mintAmount);
        }
```

Looking the above code, we will end in the `else` part of the code, and will update `Alice`'s shares to 1100. And burn 100 of her shares, which were allocated to her when she called the `deposit` function 3 years back. This means that though `Alice` locked her deposit for longer time, she ends up losing more tokens instead of gaining more. Definitely not any user(or protocol) would want. 

## Impact

User shares are not calculated correctly when the `extendLock` function is called. In most cases this will lead to burning of previously allocated tokens. And thus a loss of fund for the user.

## Code Snippet
**extendLock function:**

```solidity
    function extendLock(uint256 _depositId, uint256 _increaseDuration) external {
        // Check if actually increasing
        if (_increaseDuration == 0) {
            revert ZeroDurationError();
        }

        Deposit memory userDeposit = depositsOf[_msgSender()][_depositId];

        // Only can extend if it has not expired
        if (block.timestamp >= userDeposit.end) {
            revert DepositExpiredError();
        }
        
        // Enforce min increase to prevent flash loan or MEV transaction ordering
        uint256 increaseDuration = _increaseDuration.max(MIN_LOCK_DURATION);
        
        // New duration is the time expiration plus the increase
        uint256 duration = maxLockDuration.min(uint256(userDeposit.end - block.timestamp) + increaseDuration);

        uint256 mintAmount = userDeposit.amount * getMultiplier(duration) / 1e18;

        // Multiplier curve changes with time, need to check if the mint amount is bigger, equal or smaller than the already minted
        
        // If the new amount if bigger mint the difference
        if (mintAmount > userDeposit.shareAmount) {
            depositsOf[_msgSender()][_depositId].shareAmount =  mintAmount;
            _mint(_msgSender(), mintAmount - userDeposit.shareAmount);
        // If the new amount is less then burn that difference
        } else if (mintAmount < userDeposit.shareAmount) {
            depositsOf[_msgSender()][_depositId].shareAmount =  mintAmount;
            _burn(_msgSender(), userDeposit.shareAmount - mintAmount);
        }

        depositsOf[_msgSender()][_depositId].start = uint64(block.timestamp);
        depositsOf[_msgSender()][_depositId].end = uint64(block.timestamp) + uint64(duration);
        emit LockExtended(_depositId, _increaseDuration, _msgSender());
    }
```

## Tool used

Manual Review

## Recommendation

The `extendLock` function could be updated as follows:
```solidity
// the duration for which the deposit was active. from start to current block time.
uint256 duration = maxLockDuration.min(uint256(block.timestamp - userDeposit.start));
// what would be the mintAmount for that duration.
uint256 mintAmount = userDeposit.amount * getMultiplier(duration) / 1e18;
//update the shares amount.
depositsOf[_msgSender()][_depositId].shareAmount = mintAmount;
// now copy paste the original code from the extendLock function
uint256 increaseDuration = _increaseDuration.max(MIN_LOCK_DURATION);
// New duration is the time expiration plus the increase
duration = maxLockDuration.min(uint256(userDeposit.end - block.timestamp) + increaseDuration);
mintAmount = userDeposit.amount * getMultiplier(duration) / 1e18;
//remove the if-else block and update the shareAmount. We wont burn tokens under any circumstances
depositsOf[_msgSender()][_depositId].shareAmount +=  mintAmount;
```
Here instead of overwriting, we add the freshly minted tokens to the user's balance.
