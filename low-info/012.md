Ruhum

medium

# Pool doesn't support fee-on-transfer tokens

## Summary
Some tokens take or might take a fee on each transfer in the future. A good example is [USDT](https://etherscan.io/token/0xdac17f958d2ee523a2206206994597c13d831ec7#code#L127). The TimeLockPool contract doesn't take that into account. Any pool that uses such a token won't work as expected because of the wrong internal bookkeeping.

## Vulnerability Detail
When a user calls `deposit()` the contract expects to receive exactly `amount` tokens: https://github.com/sherlock-audit/2022-10-merit-circle/blob/main/merit-liquidity-mining/contracts/TimeLockPool.sol#L94

If `depositToken` is a fee-on-transfer token, the contract will instead receive `x` where `x < amount`. If the same user now calls `withdraw()`, the transaction will fail because the contract doesn't have enough tokens to cover the withdrawal: https://github.com/sherlock-audit/2022-10-merit-circle/blob/main/merit-liquidity-mining/contracts/TimeLockPool.sol#L133

## Impact
The contract will have fewer tokens than it expects to have. There will be no issues for initial withdrawals because the other user's deposits should cover their transactions. Later withdrawals will fail and the remaining funds will be locked inside the contract.

## Code Snippet
```sol
    function deposit(uint256 _amount, uint256 _duration, address _receiver) external override {
        if (_amount == 0) {
            revert ZeroAmountError();
        }
        // Don't allow locking > maxLockDuration
        uint256 duration = _duration.min(maxLockDuration);
        // Enforce min lockup duration to prevent flash loan or MEV transaction ordering
        duration = duration.max(MIN_LOCK_DURATION);

        depositToken.safeTransferFrom(_msgSender(), address(this), _amount);

        uint256 mintAmount = _amount * getMultiplier(duration) / 1e18;

        depositsOf[_receiver].push(Deposit({
            amount: _amount,
            shareAmount: mintAmount,
            start: uint64(block.timestamp),
            end: uint64(block.timestamp) + uint64(duration)
        }));

        _mint(_receiver, mintAmount);
        emit Deposited(_amount, duration, _receiver, _msgSender());
    }
```

## Tool used

Manual Review

## Recommendation
You can support fee-on-transfer tokens by verifying how many tokens you got from a transfer:
```sol
uint initialBalance = token.balanceOf(address(this));
token.safeTransferFrom(user, address(this), amount);
uint actualBalance = token.balanceOf(address(this)) - initialBalance; 
```
