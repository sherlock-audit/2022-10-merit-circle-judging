minhquanym

medium

# Possible DOS in function `getTotalDeposit()` and `getDepositOf()` because of unbounded gas consumption

## Summary
Both functions `getTotalDeposit()` and `getDepositOf()` loop through the whole `depositOf[_account]` array and it makes it vulnerable to unbounded gas consumption.
https://github.com/sherlock-audit/2022-10-merit-circle/blob/main/merit-liquidity-mining/contracts/TimeLockPool.sol#L248-L255

## Vulnerability Detail
Anyone can `deposit()` for victim and increase the length of `depositOf[victim]` with tiny cost. In case the `depositOf[victim]` becomes too large, functions `getTotalDeposit()` and `getDepositOf()` may be reverted if gas consumption is larger than block gas limit.

## Impact
These 2 view function is DOS, users cannot view their locks and it affects the UI.

## Code Snippet
Both functions loop through the whole array
```solidity
function getTotalDeposit(address _account) public view returns(uint256) {
    uint256 total;
    for(uint256 i = 0; i < depositsOf[_account].length; i++) {
        total += depositsOf[_account][i].amount;
    }

    return total;
}

function getDepositsOf(address _account) public view returns(Deposit[] memory) {
    return depositsOf[_account];
}
```

Anyone can deposit for another wallet with only `amount = 1 wei`
```solidity
function deposit(uint256 _amount, uint256 _duration, address _receiver) external override {
    if (_amount == 0) {
        revert ZeroAmountError();
    }
    // Don't allow locking > maxLockDuration
    uint256 duration = _duration.min(maxLockDuration);
    // Enforce min lockup duration to prevent flash loan or MEV transaction ordering
    duration = duration.max(MIN_LOCK_DURATION);

    depositToken.safeTransferFrom(_msgSender(), address(this), _amount);

    uint256 mintAmount = _amount * getMultiplier(duration) / 1e18;

    depositsOf[_receiver].push(Deposit({
        amount: _amount,
        shareAmount: mintAmount,
        start: uint64(block.timestamp),
        end: uint64(block.timestamp) + uint64(duration)
    }));

    _mint(_receiver, mintAmount);
    emit Deposited(_amount, duration, _receiver, _msgSender());
}
```

## Tool used

Manual Review

## Recommendation

Consider adding another mechanism to help to get data in range to avoid unbounded gas consumption. For example
```solidity
function getTotalDepositInRange(address _account, uint startIdx, uint endIdx) public view returns(uint256) {
    uint256 total;
    for(uint256 i = startIdx; i < endIdx; i++) {
        total += depositsOf[_account][i].amount;
    }

    return total;
}
```
