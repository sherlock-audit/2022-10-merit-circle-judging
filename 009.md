ali_shehab

medium

# Current implementation does not support DOESN’T SUPPORT FEE ON TRANSFER TOKENS

## Summary

Some ERC20 tokens are implemented so a fee is taken when transferring them, for example ```STA``` and ```PAXG```.  The current implementation of the  ```deposit``` and ```withdraw``` will mess up the accounting of the deposited amounts if the token will be a token like that, it will lead to a state where users won’t be able to receive their funds.

## Vulnerability Detail
The amount is directly added to the struct deposit. However, if for example, the STA token burns 1% of the value provided to the transfer function, so this is what will happen.
1. UserA will call deposit with the amount of 100.
2.  The contract will receive 99, but the amount added to the struct will be 100.
3. When the user now calls the withdraw functions, it will go again to the ```struct Deposit``` and transfer the amount.
4. This might succeed due to other users locking too, so the transferred tokens will be taken from “their tokens”, but in the end there will be users left without an option to withdraw their funds, because the balance of the contract will be less than the locked amount that the contract is trying to transfer.


## Impact

## Code Snippet
https://github.com/sherlock-audit/2022-10-merit-circle/blob/main/merit-liquidity-mining/contracts/TimeLockPool.sol#L99
```solidity
function deposit(uint256 _amount, uint256 _duration, address _receiver) external override {
        if (_amount == 0) {
            revert ZeroAmountError();
        }
        // Don't allow locking > maxLockDuration
        uint256 duration = _duration.min(maxLockDuration);
        // Enforce min lockup duration to prevent flash loan or MEV transaction ordering
        duration = duration.max(MIN_LOCK_DURATION);

        depositToken.safeTransferFrom(_msgSender(), address(this), _amount);

        uint256 mintAmount = _amount * getMultiplier(duration) / 1e18;

        depositsOf[_receiver].push(Deposit({
            amount: _amount,
            shareAmount: mintAmount,
            start: uint64(block.timestamp),
            end: uint64(block.timestamp) + uint64(duration)
        }));

        _mint(_receiver, mintAmount);
        emit Deposited(_amount, duration, _receiver, _msgSender());
    }
```
https://github.com/sherlock-audit/2022-10-merit-circle/blob/main/merit-liquidity-mining/contracts/TimeLockPool.sol#L133
```solidity
function withdraw(uint256 _depositId, address _receiver) external {
        if (_depositId >= depositsOf[_msgSender()].length) {
            revert NonExistingDepositError();
        }
        Deposit memory userDeposit = depositsOf[_msgSender()][_depositId];
        if (block.timestamp < userDeposit.end) {
            revert TooSoonError();
        }

        // remove Deposit
        depositsOf[_msgSender()][_depositId] = depositsOf[_msgSender()][depositsOf[_msgSender()].length - 1];
        depositsOf[_msgSender()].pop();

        // burn pool shares
        _burn(_msgSender(), userDeposit.shareAmount);
        
        // return tokens
        depositToken.safeTransfer(_receiver, userDeposit.amount);
        emit Withdrawn(_depositId, _receiver, _msgSender(), userDeposit.amount);
    }

```

## Tool used

Manual Review

## Recommendation
Calculate the amount to add to the locked amount by the difference between the balances before and after the transfer instead of using the supplied value.
