hyh

high

# Maximum duration multiplier can be forced for lock amount increase

## Summary

Duration multiplier is calculated based on the sender's lock data, while the amount actually received is locked for the receiver's lock period.

This provides a surface for the manipulation of locking the increaseLock() amount with the big multiplier, while it's actually locked for the small duration.

## Vulnerability Detail

Bob the attacker can create two accounts, lock a small amount, say `1 USDC`, with the first for the `maxLockDuration`, then lock some amount, say the same `1 USDC`, with the second account for the `MIN_LOCK_DURATION`, which is only 10 minutes.

Then Bob calls `increaseLock(id, second, 1e6 USDC)` from the first account, locking the whole amount for `MIN_LOCK_DURATION` with the maximum multiplier from `maxLockDuration`.

## Impact

Net impact here is reward stealing from all other users, as the shares being issued to the attacker will be substantially inflated. This way the severity is high.

## Code Snippet

Currently `_msgSender()`'s deposit is used to obtain the lock's end time:

https://github.com/sherlock-audit/2022-10-merit-circle/blob/main/merit-liquidity-mining/contracts/TimeLockPool.sol#L197-L222

```solidity
    function increaseLock(uint256 _depositId, address _receiver, uint256 _increaseAmount) external {
        // Check if actually increasing
        if (_increaseAmount == 0) {
            revert ZeroAmountError();
        }

        Deposit memory userDeposit = depositsOf[_msgSender()][_depositId];

        // Only can extend if it has not expired
        if (block.timestamp >= userDeposit.end) {
            revert DepositExpiredError();
        }

        depositToken.safeTransferFrom(_msgSender(), address(this), _increaseAmount);

        // Multiplier should be acording the remaining time to the deposit to end
        uint256 remainingDuration = uint256(userDeposit.end - block.timestamp);

        uint256 mintAmount = _increaseAmount * getMultiplier(remainingDuration) / 1e18;

        depositsOf[_receiver][_depositId].amount += _increaseAmount;
        depositsOf[_receiver][_depositId].shareAmount += mintAmount;

        _mint(_receiver, mintAmount);
        emit LockIncreased(_depositId, _receiver, _msgSender(), _increaseAmount);
    }
```

This way the duration remaining is calculated off `_msgSender()`'s data, not `receiver`'s: `mintAmount` is `_increaseAmount * getMultiplier(remainingDuration) / 1e18`, while `remainingDuration = uint256(depositsOf[_msgSender()][_depositId].end - block.timestamp)`, so an `_increaseAmount` can receive, for example, maximal `getMultiplier` based on big `_msgSender()` lock's `end`, while actual locking will be based on `depositsOf[receiver][_depositId].end`, which stays the same and can be as low as `MIN_LOCK_DURATION`:

https://github.com/sherlock-audit/2022-10-merit-circle/blob/main/merit-liquidity-mining/contracts/TimeLockPool.sol#L22

```solidity
   uint256 public constant MIN_LOCK_DURATION = 10 minutes;
```

After that the rewards can be withdrawn immediately:

https://github.com/sherlock-audit/2022-10-merit-circle/blob/main/merit-liquidity-mining/contracts/base/BasePool.sol#L100-L112

```solidity
    function claimRewards(address _receiver) external {
        uint256 rewardAmount = _prepareCollect(_msgSender());
        uint256 escrowedRewardAmount = rewardAmount * escrowPortion / 1e18;
        uint256 nonEscrowedRewardAmount = rewardAmount - escrowedRewardAmount;

        if(escrowedRewardAmount != 0 && address(escrowPool) != address(0)) {
            escrowPool.deposit(escrowedRewardAmount, escrowDuration, _receiver);
        }

        // ignore dust
        if(nonEscrowedRewardAmount > 1) {
            rewardToken.safeTransfer(_receiver, nonEscrowedRewardAmount);
        }
```

https://github.com/sherlock-audit/2022-10-merit-circle/blob/main/merit-liquidity-mining/contracts/base/AbstractRewards.sol#L106-L107

```solidity
  function _prepareCollect(address _account) internal returns (uint256) {
    uint256 _withdrawableDividend = withdrawableRewardsOf(_account);
```

https://github.com/sherlock-audit/2022-10-merit-circle/blob/main/merit-liquidity-mining/contracts/base/AbstractRewards.sol#L53-L55

```solidity
  function withdrawableRewardsOf(address _account) public view override returns (uint256) {
    return cumulativeRewardsOf(_account) - withdrawnRewards[_account];
  }
```

https://github.com/sherlock-audit/2022-10-merit-circle/blob/main/merit-liquidity-mining/contracts/base/AbstractRewards.sol#L73-L75

```solidity
  function cumulativeRewardsOf(address _account) public view override returns (uint256) {
    return ((pointsPerShare * getSharesOf(_account)).toInt256() + pointsCorrection[_account]).toUint256() / POINTS_MULTIPLIER;
  }
```
## Tool used

Manual Review

## Recommendation

Consider using the deposit record of a `_receiver`:

https://github.com/sherlock-audit/2022-10-merit-circle/blob/main/merit-liquidity-mining/contracts/TimeLockPool.sol#L197-L222

```solidity
    function increaseLock(uint256 _depositId, address _receiver, uint256 _increaseAmount) external {
        // Check if actually increasing
        if (_increaseAmount == 0) {
            revert ZeroAmountError();
        }

-       Deposit memory userDeposit = depositsOf[_msgSender()][_depositId];
+       Deposit memory userDeposit = depositsOf[_receiver][_depositId];

        // Only can extend if it has not expired
        if (block.timestamp >= userDeposit.end) {
            revert DepositExpiredError();
        }

        depositToken.safeTransferFrom(_msgSender(), address(this), _increaseAmount);

        // Multiplier should be acording the remaining time to the deposit to end
        uint256 remainingDuration = uint256(userDeposit.end - block.timestamp);

        uint256 mintAmount = _increaseAmount * getMultiplier(remainingDuration) / 1e18;

        depositsOf[_receiver][_depositId].amount += _increaseAmount;
        depositsOf[_receiver][_depositId].shareAmount += mintAmount;

        _mint(_receiver, mintAmount);
        emit LockIncreased(_depositId, _receiver, _msgSender(), _increaseAmount);
    }
```
